apply plugin: 'com.android.application'

android {
    compileSdkVersion 27
    buildToolsVersion "27.0.3"

    byte verMajor  = 1;
    byte verMiddle = 2;
    byte verMinor  = 0;

    defaultConfig {
        applicationId "jp.toastkid.search_widget"
        minSdkVersion 16
        targetSdkVersion 27
        if (verMiddle >= 100 || verMinor >= 100) {
            throw new IllegalStateException("Middle and Minor version value is allowed under 100.")
        }
        versionCode ((verMajor * 10000) + (verMiddle * 100) + verMinor)
        versionName verMajor + "." + verMiddle + "." + verMinor
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    def supportLibVersion = '27.1.1'
    implementation 'com.android.support:appcompat-v7:' + supportLibVersion
    implementation 'com.android.support:design:'       + supportLibVersion
    implementation 'com.android.support:customtabs:'   + supportLibVersion
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'

    def firebaseVersion = '11.6.2'
    implementation 'com.google.firebase:firebase-core:'  + firebaseVersion
    implementation 'com.google.firebase:firebase-ads:'   + firebaseVersion
    implementation 'com.google.firebase:firebase-crash:' + firebaseVersion

    implementation 'com.larswerkman:HoloColorPicker:1.5'
    implementation 'com.jakewharton:butterknife:8.6.0'
    implementation 'com.squareup.okhttp3:okhttp:3.8.0'
    implementation 'io.reactivex.rxjava2:rxjava:2.1.0'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'
    implementation 'com.github.gfx.android.orma:orma:4.2.5'

    annotationProcessor 'com.github.gfx.android.orma:orma-processor:4.2.5'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.6.0'

    def leakcanaryVersion = '1.5.1'
    debugImplementation   'com.squareup.leakcanary:leakcanary-android:'       + leakcanaryVersion
    releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:' + leakcanaryVersion

    testImplementation 'junit:junit:4.12'
}

apply plugin: 'com.google.gms.google-services'
